{
    --------------------------------------------
    Filename: protocol.net.tcp.spin2
    Author: Jesse Burt
    Description: Transmission Control Protocol
    Started Apr 10, 2022
    Updated Apr 19, 2022
    Copyright 2022
    See end of file for terms of use.
    --------------------------------------------
}
#ifndef NET_COMMON
#include "net-common.spin2h"
#endif

CON

    { limits }
    TCP_HDR_SZ      = 20

    { offsets within header }
    TCPH_SRCP        = 0
    TCPH_DESTP       = TCPH_SRCP+2
    TCPH_SEQNR       = TCPH_DESTP+2
    TCPH_ACKNR       = TCPH_SEQNR+4
    TCPH_HDRLEN      = TCPH_ACKNR+4
    TCPH_FLAGS       = TCPH_ACKNR+4
    TCPH_WIN         = TCPH_FLAGS+2
    TCPH_CKSUM       = TCPH_WIN+2
    TCPH_URGPTR      = TCPH_CKSUM+2
    TCPH_OPTS        = TCPH_URGPTR+2

    { TCP flags/control bits }
    NONCE           = 8
    CWR             = 7
    ECN_ECHO        = 6
    URG             = 5
    ACK             = 4
    PSH             = 3
    RST             = 2
    SYN             = 1
    FIN             = 0

    NONCE_BIT       = 1 << NONCE
    CWR_BIT         = 1 << CWR
    ECN_ECHO_BIT    = 1 << ECN_ECHO
    URG_BIT         = 1 << URG
    ACK_BIT         = 1 << ACK
    PSH_BIT         = 1 << PSH
    RST_BIT         = 1 << RST
    SYN_BIT         = 1 << SYN
    FIN_BIT         = 1 << FIN

    { TCP options }
    NOOP            = $01
    MSS             = $02
    WIN_SCALE       = $03
    SACK_PRMIT      = $04
    TMSTAMPS        = $08

    PSRC            = 0                         ' _tcp_port.word[n]
    PDEST           = 1

VAR

    long _seq_nr, _ack_nr
    long _tmstamps[2]
    long _tcp_port
    word _tcp_cksum
    word _tcp_win
    word _urg_ptr
    word _tcp_flags
    word _tcp_msglen
    word _tcp_mss
    byte _tcp_hdrlen
    byte _options_len
    byte _tcp_winscale
    byte _tcp_sack_perm

PUB TCP_Reset()
' Reset/initialize all stored data to 0
    longfill(@_seq_nr, 0, 5)
    wordfill(@_tcp_cksum, 0, 6)
    bytefill(@_tcp_hdrlen, 0, 4)

PUB TCP_SetAckNr(ack_nr)
' Set TCP acknowledgement number
    _ack_nr := ack_nr

PUB TCP_SetChksum(ck)
' Set checksum
    _tcp_cksum := ck

PUB TCP_SetDestPort(p)
' Set destination port field
    _tcp_port.word[PDEST] := p

PUB TCP_SetFlags(flags)
' Set TCP header flags
    _tcp_flags := flags

PUB TCP_SetHdrLen(length)
' Set TCP header length, in longs
    _tcp_hdrlen := length << 4

PUB TCP_SetHdrLenBytes(length)
' Set TCP header length, in bytes
'   NOTE: length must be a multiple of 4
    _tcp_hdrlen := (length / 4) << 4

PUB TCP_SetMSS(sz)
' Set TCP maximum segment size
    _tcp_mss := sz

PUB TCP_SetSACKPerm(sp)
' Set selective-acknowledge permitted flag
    { equal to the length in a SACK_PERMIT KLV option? }
    _tcp_sack_perm := (sp <> 0)

PUB TCP_SetSeqNr(seq_nr)
' Set TCP sequence number
    _seq_nr := seq_nr

PUB TCP_SetSrcPort(p)
' Set source port field
    _tcp_port.word[PSRC] := p

PUB TCP_SetTimeSt(tm)
' Set timestamp (TCP option)
    _tmstamps[0] := tm

PUB TCP_SetTimeSt_Echo(tm)
' Set timestamp echo (TCP option)
    _tmstamps[1] := tm

PUB TCP_SetUrgentPtr(uptr)
' Set TCP urgent pointer
    _urg_ptr := uptr

PUB TCP_SetWindow(win)
' Set TCP window
    _tcp_win := win

PUB TCP_IncAckNr(amt)
' Increment acknowledgement number by amt
    _ack_nr += amt

PUB TCP_IncSeqNr(amt)
' Increment sequence number by amt
    _seq_nr += amt

PUB TCP_AckNr(): ack_nr
' Get TCP acknowledgement number
    return _ack_nr

PUB TCP_Chksum(): ck
' Get TCP header checksum
    return _tcp_cksum

PUB TCP_DestPort(): p
' Get destination port field
    return _tcp_port.word[PDEST]

PUB TCP_Flags(): flags
' Get TCP header flags
    return _tcp_flags

PUB TCP_HdrLen(): len
' Get current header length, in longs
'   NOTE: Length is stored in as-received position (upper nibble)
    return _tcp_hdrlen

PUB TCP_HdrLenBytes(): len
' Get current header length, in bytes
    return (_tcp_hdrlen >> 4) * 4

PUB TCP_MsgLen(): len
' Get current message length
    return _tcp_msglen

PUB TCP_MSS(): mss
' Get current maximum segment size (TCP option)
    return _tcp_mss

PUB TCP_SACKPerm(): sp
' Get selective-acknowledge permitted flag (TCP option)
    { equal to the length in a SACK_PERMIT KLV option? }
    return (_tcp_sack_perm == 2)

PUB TCP_SeqNr(): seq_nr
' Get TCP sequence number
    return _seq_nr

PUB TCP_SrcPort(): p
' Get source port field
    return _tcp_port.word[PSRC]

PUB TCP_SwapPorts()
' Swap source and destination ports, for use when sending a response
    _tcp_port ror= 16

PUB TCP_SwapSeqNrs{} | tmp
' Swap sequence and acknowledgement numbers, for use when sending a response
    tmp := _seq_nr
    _seq_nr := _ack_nr
    _ack_nr := tmp

PUB TCP_TimeSt(): tm
' Get timestamp (TCP option)
    return _tmstamps[0]

PUB TCP_TimeSt_Echo(): tm
' Get timestamp echo (TCP option)
    return _tmstamps[1]

PUB TCP_TimeSt_Ptr(): ptr
' Get pointer to timestamp data
    return @_tmstamps

PUB TCP_UrgentPtr(): uptr
' Get TCP urgent pointer
    return _urg_ptr

PUB TCP_Window(): win
' Get TCP header window
    return _tcp_win

PUB Rd_TCP_Header() | tmp
' Read/disassemble TCP header
'   Returns: length of read header, in bytes
    _tcp_port.word[PSRC] := rdword_msbf()
    _tcp_port.word[PDEST] := rdword_msbf()
    _seq_nr := rdlong_msbf()
    _ack_nr := rdlong_msbf()
    tmp := rd_byte()
        _tcp_hdrlen := tmp & $f0                ' [7..4]: hdr len (longs)
                                                ' [3..1]: reserved
        _tcp_flags := (tmp & 1) << NONCE        ' [0]   : TCP flags[8]
    tmp := rd_byte()
        _tcp_flags |= tmp                       ' [7..0]: TCP flags[7..0]
    _tcp_win := rdword_msbf()
    _tcp_cksum := rdword_msbf()
    _urg_ptr := rdword_msbf()

    return currptr()

PUB Rd_TCP_Opts(): ptr | kind, st, opts_len
' Read TCP options
    st := currptr()

    { TCP header length is the header itself plus the options; }
    {   subtract out the header to get the length of the options }
    opts_len := tcp_hdrlenbytes() - 20

    { read through all KLVs }
    repeat
        kind := rd_byte()
        case kind
            MSS:
                rd_byte()                       ' skip over the length byte
                _tcp_mss := rdword_msbf()
            SACK_PRMIT:
                _tcp_sack_perm := rd_byte()     ' actually the length byte
            TMSTAMPS:
                rd_byte()
                _tmstamps[0] := rdlong_msbf()
                _tmstamps[1] := rdlong_msbf()
            NOOP:
                ' only one byte - do nothing
            WIN_SCALE:
                _tcp_winscale := rd_byte()
    until (currptr()-st) > opts_len
    return currptr()

PUB Wr_TCP_Header(): ptr | st
' Write/assemble TCP header
'   Returns: length of assembled header, in bytes
    st := currptr()
    wrword_msbf(_tcp_port.word[PSRC])
    wrword_msbf(_tcp_port.word[PDEST])
    wrlong_msbf(_seq_nr)
    wrlong_msbf(_ack_nr)
    wr_byte(_tcp_hdrlen | ((_tcp_flags >> NONCE) & 1))   ' XXX | _tcp_flags.byte[1] ?
    wr_byte(_tcp_flags & $ff) ' XXX _tcp_flags.byte[0] ?
    wrword_msbf(_tcp_win)
    wrword_msbf(_tcp_cksum)
    wrword_msbf(_urg_ptr)

    _tcp_msglen := currptr()-st
    return _tcp_msglen

PUB WriteKLV(kind, len, wr_val, val, byte_ord): tlvlen
' Write KLV to ptr_buff
'   kind:
'       option kind
'   len:
'       length of KLV, 1..255 (includes kind byte, length byte, and
'           number of option bytes)
'       any other value: only the type will be written
'   wr_val:
'       Whether to write the value data (0: ignore, non-zero: write val)
'   val:
'       value of option data
'       When len is 1..4, values will be read directly from parameter
'       When len is 5..255, val is expected to be a pointer to value data
'       (ignored if len is outside valid range or if wr_val is 0)
'   byte_ord:
'       byte order to write option data (LSBF or MSBF)
'   Returns: total length of KLV (includes: kind, length, and all values)

    { track length of options; it'll be needed later for padding
        the end of the message }
    _options_len += wr_byte(kind)
    case len
        { immediate value }
        1..4:
            _options_len += wr_byte(len)        ' write length byte
            { only write the value data if explicitly called to; }
            {   some options only consist of the TYPE and LENGTH fields }
            if (wr_val)                         ' write value
                if (byte_ord == LSBF)
                    _options_len += wrblk_lsbf(@val, len-2)
                else
                    _options_len += wrblk_msbf(@val, len-2)
        { values pointed to }
        5..255:
            _options_len += wr_byte(len)
            if (wr_val)
                if (byte_ord == LSBF)
                    _options_len += wrblk_lsbf(val, len-2)
                else
                    _options_len += wrblk_msbf(val, len-2)
        { write type only }
        other:
    return _options_len

DAT

{
TERMS OF USE: MIT License

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
}

